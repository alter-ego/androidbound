buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'maven'

ext {
    buildVersionName = "0.3.3"
    buildVersionCode = 17
}

repositories {
    mavenCentral()
    maven() { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url "https://github.com/alter-ego/advanced-android-logger/raw/develop/releases/" }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.alterego:advancedandroidlogger:1.0.4@aar'

    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:support-v4:22.0.0'

    compile 'joda-time:joda-time:2.7'
    compile 'io.reactivex:rxandroid:0.24.0'

    provided 'javax.annotation:jsr250-api:1.0'
    compile 'org.projectlombok:lombok:1.16.2'

    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
}

android {
    compileSdkVersion 22
    buildToolsVersion "22"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode buildVersionCode
        versionName buildVersionName
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

}

// This is the actual solution, as in http://stackoverflow.com/a/19037807/1002054
task clearJar(type: Delete) {
    delete 'build/libs/androidbound.jar'
}

task makeJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('releases/')
    include('classes.jar')
    rename('classes.jar', 'androidbound.jar')
}

makeJar.dependsOn(clearJar, build)

uploadArchives {
    repositories.mavenDeployer {
        def deployPath = file(getProperty('aar.deployPath'))
        repository(url: "file://${deployPath.absolutePath}")
        pom.project {
            groupId 'com.alterego'
            artifactId 'androidbound'
            version buildVersionName
        }
    }
}